/*******************************************************************************
 * Copyright 2011 See AUTHORS file.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 ******************************************************************************/

sourceSets.main.java.srcDirs = [
        "jni/swig-src/collision",
        "jni/swig-src/dynamics",
        "jni/swig-src/extras",
        "jni/swig-src/linearmath",
        "jni/swig-src/softbody",
        "jni/swig-src/inversedynamics",
        "src"
]

dependencies {
    compileOnly project(":gdx")
    compileOnly project(":extensions:gdx-jnigen")
}

import com.badlogic.gdx.jnigen.BuildTarget
import com.badlogic.gdx.jnigen.BuildTarget.TargetOs
import com.badlogic.gdx.jnigen.gradle.JnigenTask

task jnigen (type: JnigenTask) {
    sharedLibName = "gdx-bullet"
    all {
        headerDirs = ["src/bullet/", "src/custom/", "src/extras/Serialize/", "src/extras/"]
        cExcludes = ["src/bullet/BulletMultiThreaded/GpuSoftBodySolvers/**"]
        cppExcludes = ["src/bullet/BulletMultiThreaded/GpuSoftBodySolvers/**"]
        
        // SWIG doesn't emit strict aliasing compliant code
        cppFlags += " -fno-strict-aliasing";
        // SWIG directors aren't clearly documented to require RTTI, but SWIG
        // normally generates a small number of dynamic_casts for director code.
        // gdx-bullet's swig build.xml replaces these with static C casts so we
        // can compile without RTTI and save some disk space. It seems to work
        // with these static casts.
        cppFlags += " -fno-rtti";
        // Disable profiling (it's on by default). If you change this, you
        // must regenerate the SWIG wrappers with the changed value.
        cppFlags += " -DBT_NO_PROFILE";
        //Bullet 2 compatibility with inverse dynamics
        cppFlags += " -DBT_USE_INVERSE_DYNAMICS_WITH_BULLET2";
    }
    add(TargetOs.Windows, false)
    add(TargetOs.Windows, true)
    add(TargetOs.Linux, false)
    add(TargetOs.Linux, true)
    add(TargetOs.MacOsX, true)
    add(TargetOs.Android, false) {
        cppFlags += " -fexceptions"
        //TODO: Missing new FileHandle(new File("jni/Application.mk")).writeString("\nAPP_STL := stlport_static\n", true);
    }
    add(TargetOs.IOS, false) {
        cppFlags += " -stdlib=libc++";
    }
}