/*******************************************************************************
 * Copyright 2011 See AUTHORS file.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 ******************************************************************************/

apply plugin: "java"

sourceCompatibility = 1.6
targetCompatibility = 1.6

sourceSets.main.java.srcDirs = ["src"]
sourceSets.main.resources.srcDirs = ["src"]
sourceSets.test.java.srcDirs = ["test"]

compileJava {
    options.fork = true
    options.incremental = true
}

dependencies {
    compileOnly project(":extensions:gdx-jnigen")
    testCompile libraries.junit
}

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

import com.badlogic.gdx.jnigen.BuildTarget
import com.badlogic.gdx.jnigen.BuildTarget.TargetOs
import com.badlogic.gdx.jnigen.gradle.JnigenTask

task jnigen (type: JnigenTask) {
    sharedLibName = "gdx"
    temporaryDir = "../target/native"
    all {
        cppExcludes = ["iosgl/**"]
    }
    add(TargetOs.Windows, false)
    add(TargetOs.Windows, true)
    add(TargetOs.Linux, false)
    add(TargetOs.Linux, true)
    add(TargetOs.Android, false) {
        linkerFlags += " -llog"
    }
    add(TargetOs.MacOsX, true)
    add(TargetOs.IOS, false) {
        headerDirs = ["iosgl"]
        cppExcludes = []
    }
}